buildscript {
    repositories {
        mavenCentral()
        maven {
            // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/maven2/"
        }
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradleVersion}"
    }
}

plugins {
    id 'com.google.protobuf' version "${protobufGradleVersion}" apply false
    id 'com.github.johnrengelman.shadow' version "${shadowVersion}" apply false
}

allprojects {

    repositories {
        mavenLocal()
        maven {
            url "https://maven-central.storage-download.googleapis.com/maven2/"
        }
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'com.google.protobuf'

    dependencies {
        implementation group: 'javax.annotation', name: 'javax.annotation-api', version: "${javaxannotationVersion}"
        implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: "${protobufVersion}"
        implementation "io.grpc:grpc-protobuf:${grpcVersion}"
        implementation "io.grpc:grpc-stub:${grpcVersion}"


        implementation "org.slf4j:slf4j-api:${slf4jVersion}"
        implementation "org.slf4j:slf4j-simple:${slf4jVersion}"

        testImplementation "junit:junit:${junitVersion}"
        testImplementation group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
        testImplementation group: 'org.mockito', name: 'mockito-inline', version: "${mockitoVersion}"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = false
            csv.required = false
        }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/oscar-besga-panel/LockFactoryServer")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

group 'org.obapanel.lockfactoryserver'
version "${projectVersion}"

sourceCompatibility = "${javaVersion}"
targetCompatibility = "${javaVersion}"

